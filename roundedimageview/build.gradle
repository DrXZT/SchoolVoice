apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'
def sonatypeUsername = project.hasProperty('sonatypeUsername') ? sonatypeUsername : ""
def sonatypePassword = project.hasProperty('sonatypePassword') ? sonatypePassword : ""
android {
    compileSdkVersion 28



    defaultConfig {

        minSdkVersion 17
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {

    compileOnly 'com.squareup.picasso:picasso:2.5.2'
    compileOnly 'com.android.support:support-annotations:25.1.1'
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:28.0.0-rc01'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.source
}

task androidJavadocsJar(type: Jar) {
    classifier = 'javadoc'
    //basename = artifact_id
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    //basename = artifact_id
    from android.sourceSets.main.java.source
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        pom {
            project {
                name 'RoundedImageView'
                packaging 'aar'

                description 'Fast ImageView with support for rounded corners and borders'
                url 'https://github.com/vinc3m1/RoundedImageView'

                scm {
                    url 'scm:git@github.com:vinc3m1/RoundedImageView.git'
                    connection 'scm:git@github.com:vinc3m1/RoundedImageView.git'
                    developerConnection 'scm:git@github.com:vinc3m1/RoundedImageView.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'vinc3m1'
                        name 'Vince Mi'
                        email 'vince@makeramen.com'
                    }
                }
            }
        }
    }
}

